using System;
using System.Linq;
using Gibberish.AST;
using Gibberish.AST._1_Bare;

ironmeta RecognizeBlocks<char, LanguageConstruct> : IronMeta.Matcher.Matcher<char, LanguageConstruct>
{
	WholeFile = LanguageConstruct*:matches;
	LanguageConstruct = BlankLine | CommentDefinition | Prelude | Statement;

	Statement = '\t'*:indentation (~(':' | '\n' | '\r') .)*:content NL -> { return _ExtractStatementAndErrors(indentation.Inputs.Count(), content.AsString()); };

	Prelude = '\t'*:indentation (~(':' | '\n' | '\r') .)*:content ":" (~('\n' | '\r') .)*:trailingContent NL -> { return _ExtractPreludeAndErrors(indentation.Inputs.Count(), content.AsString(), trailingContent.AsString()); };

	CommentDefinition = '#' ('[' /[0-9]/+:commentId "]:")? (' ' | '\t')*:commentSep (~('\r' | '\n') .)*:content -> { return _ExtractSingleLineCommentDefinition(commentId.AsString(), content.AsString(), commentSep.AsString()); };
	
	BlankLine = /[\t ]/* NL;

	Nothing = (~(.?));
	NL = "\r\n" | "\n" | "\r";
}